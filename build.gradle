plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.31"
}

group = 'com.anaplan.engineering'
version = '3.0.2'

apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

ext {
    kotlinVersion = "1.5.31"
    overtureVersion = "3.0.2"
}

repositories {
    mavenCentral()
}

sourceSets {
    functionalTest {
        kotlin.srcDir 'src/test-fn/kotlin'
        resources.srcDir 'src/test-fn/resources'
    }
}

task functionalTestClasspathJar(type: Jar) {
    inputs.files(sourceSets.test.runtimeClasspath)
        .withPropertyName("runtimeClasspath")
        .withNormalizer(ClasspathNormalizer)
    archiveFileName = "functionalTestClasspath.jar"
    doFirst {
        manifest {
            def classpath = sourceSets.test.runtimeClasspath.files
            attributes "Class-Path": classpath.collect { f -> f.toURI().toString() }.join(" ")
        }
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
}

dependencies {
    implementation gradleApi()
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-html-jvm', version: '0.7.3'

    implementation group: 'org.overturetool.core', name: 'interpreter', version: overtureVersion
    implementation group: 'org.overturetool.core.annotations', name: 'provided', version: overtureVersion

    implementation group: 'com.anaplan.engineering', name: 'vdm-pretty-printer', version: overtureVersion

    implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.11.0'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark-ext-gfm-tables', version: '0.9.0'

    implementation group: 'com.xenomachina', name: 'kotlin-argparser', version: '2.0.7'

    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: kotlinVersion
    testImplementation group: 'org.mockito.kotlin', name: 'mockito-kotlin', version: '4.0.0'

    functionalTestImplementation gradleTestKit()
}


task functionalTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    systemProperty "functionalTestClasspathJar", functionalTestClasspathJar.archivePath.absolutePath
    outputs.upToDateWhen { false }
    mustRunAfter test
    dependsOn functionalTestClasspathJar
}

check.dependsOn functionalTest

gradlePlugin {
    plugins {
        vdm {
            id = "vdm"
            implementationClass = "com.anaplan.engineering.vdmgradleplugin.VdmGradlePlugin"
        }
    }
}

// Using artifactory plugin appears broken -- https://github.com/gradle/gradle/issues/4783
publishing {
    repositories {
        maven {
            credentials {
                username System.env.ART_USERNAME
                password System.env.ART_PASSWORD
            }
            url findProperty('ART_URL')
        }
    }
}
